apply plugin: 'com.android.application'

android {

    //Required to support the old folder structure
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        androidTest.setRoot('tests')
    }

    //Required to support builds although lint errors exist
    lintOptions {
        abortOnError false
    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.orangelabs.rcs.core.control"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "3.1.1"
        archivesBaseName = "core_control"
    }
}

dependencies {
    compile project(':com.gsma.services.rcs')
}

//There is a dependecy on src belonging to RI
task copyFiles(type:Copy) {
    from('./../../RI/src/') {
        include 'com/orangelabs/rcs/ri/ConnectionManager*'
        include 'com/orangelabs/rcs/ri/utils/LockAccess*'
        include 'com/orangelabs/rcs/ri/utils/LogUtils*'
    }
    into 'src'
    includeEmptyDirs = true
}
preBuild.dependsOn copyFiles

task deleteFiles(type: Delete) {
  delete 'src/com/orangelabs/rcs/ri'
}
clean.dependsOn deleteFiles

//Below install dependecy was added to always install RCS service before
//a RCS client to secure that Android handles RCS permissions correctly.
task installServiceFirst(dependsOn: ':core:installDebug') << {
    println 'RCS core service was installed first!'
}
tasks.whenTaskAdded { task ->
    if (task.name == 'installDebug') {
        task.dependsOn installServiceFirst
    }
}