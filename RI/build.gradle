apply plugin: 'com.android.application'

android {

    //Required to support the old folder structure
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        androidTest.setRoot('tests')
    }

    //Required to support builds although lint errors exist
    lintOptions {
        abortOnError false
    }

    //Google maps
    //compileSdkVersion rootProject.compileSdkVersion
    compileSdkVersion "Google Inc.:Google APIs:21"
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.orangelabs.rcs.ri"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "3.1.1"
        archivesBaseName = "RI"
    }
}

dependencies {
    compile project(':ApiLib')
    compile project(':mediaplayer')
    compile 'com.android.support:support-v4:21.0.0'
    //Use offical version above instead of local version below
    //compile files('libs/android-support-v4.jar')
}

//Below install dependecy was added to always install RCS service before
//a RCS client to secure that Android handles RCS permissions correctly.
task installServiceFirst(dependsOn: ':core:installDebug') << {
    println 'RCS core service was installed first!'
}
tasks.whenTaskAdded { task ->
    if (task.name == 'installDebug') {
        task.dependsOn installServiceFirst
    }
}